sep ::= [ \t]+
break ::= sep? ((";" | "\n") sep?)+
lineBreak ::= sep? ("\n" sep?)+
lineSep ::= lineBreak | sep

root ::= break? (statement break?)*
statement ::= declaration | assignment | expr | returnStatement
returnStatement ::= "return" (sep expr)?
declaration ::= "$" sep? name sep? "=" sep? expr
assignment ::= expr sep? "=" sep? expr

binaryOperator ::= "||" | "&&" | "==" | "<=" | ">=" | [-+*/^<>]
unaryOperator ::= [-!]

expr ::= term (sep? binaryOperator sep? term)*
enclosedExpr ::= "(" sep? expr sep? ")"
term ::= (unaryOperator sep?)* value sep? tailOperationChain?
lambdaParameter ::= "$"
value ::= enclosedExpr | name | literal | lambdaParameter

alpha ::= [a-z] | [A-Z]
alphanum ::= [a-z] | [A-Z] | [0-9]
name ::= ("_" | alpha) alphanum*

literal ::= float | int | boolean | string | list | function
int ::= "-"? [0-9]+
list ::= "[" lineSep? (expr (lineSep? "," lineSep? expr)*)? lineSep? "]"
float ::= ("-"? [0-9]+)? "." [0-9]+
string ::= "\"" (escapeSequence | [^"])* "\""
escapeSequence ::= "\\" [^]
boolean ::= "true" | "false"

invoke ::= invokeArgs | functionBody
invokeArgs ::= "(" sep? (expr sep? ("," sep? expr)*)? sep? ")"

functionArg ::= name (sep? ":" sep? name)?
functionBody ::= "{" break? (statement break)* statement? break? "}"
functionArgs ::= "(" sep? (functionArg sep? ("," sep? functionArg)*)? sep? ")"
function ::= functionArgs? sep? functionBody

receiverCall ::= "." lineSep? name sep? invoke
index ::= "[" expr "]"
tailOperation ::= invoke | receiverCall | index
tailOperationChain ::= tailOperation (sep? tailOperation)*